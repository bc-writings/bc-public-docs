% Source: https://www.mathematex.fr/viewtopic.php?p=162541#p162541

\documentclass{article}
\usepackage{luamplib}

\newcommand{\domino}[2]{\leavevmode\begin{mplibcode}
u := 1cm;
def drawdots(expr n,p) =
    if n mod 2 <> 0:
        drawdot(p+(u/2,u/2));
    fi;
    if n > 1:
        drawdot(p+( u/4, u/4));
        drawdot(p+(3u/4,3u/4));
    fi;
    if n > 3:
    	drawdot(p+(u/4,3u/4));
        drawdot(p+(3u/4,u/4));
    fi;
    if n > 5:
    	drawdot(p+( u/4,u/2));
    	drawdot(p+(3u/4,u/2));
    fi;
enddef;

def domino(expr a,b) =
    pickup pencircle scaled 1pt;
    	draw (0,0) -- (2u,0) -- (2u,u) -- (0,u) -- cycle;
    	draw (u,0) -- (u,u);
    
    pickup pencircle scaled 5pt;
    	drawdots(a,(0,0));
    	drawdots(b,(u,0));
enddef;

beginfig(1);
	domino(#1,#2);
endfig;
\end{mplibcode}}

\begin{document}

\begin{center}
\begin{tabular}{cccccc}
\domino{0}{1} & \domino{1}{2} & \domino{2}{3} & \domino{3}{4} & \domino{4}{5} & \domino{5}{6}
\end{tabular}
\end{center}

\end{document}